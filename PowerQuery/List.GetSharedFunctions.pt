let 
    doc = [
        Documentation.Name = "get Shared Functions", 
        Documentation.FunctionName = "List.GetSharedFunctions",
        Documentation.Description = "Create a list of functions that can be copied into the Github functions template. Simply copy this function in a blank query and update the 'SelectionList' list to omit any parameters and custom fumctions. You can either include (Allow) or excluse (Disallow) items. The function checks whether the record name starts with any of the values. Shouldn't be needed often, but on occasions additional functions might be added by Microsoft or tool suppliers you might need. "
        ],
    fn = () => 
        let
    SelectionListType = "Allow", //Allow or Disallow
    SelectionList = {
            "AccessControlEntry","AccessControlKind",
            "AI","AIFunctions", "Any","Binary","BinaryEncoding","BinaryFormat",
            "BinaryOccurrence","BufferMode","Byte","ByteOrder",
            "Character","Combiner","Comparer",
            "Compression","Csv","CsvStyle",
            "Culture","Currency","Date",
            "DateTime","DateTimeZone","Day",
            "Decimal","Diagnostics","DirectQueryCapabilities",
            "Double","Duration","Embedded","Error",
            "Expression","ExtraValues","File",
            "Folder","Function","Graph","GroupKind",
            "Guid","Html","Identity","IdentityProvider",
            "Int16","Int32","Int64","Int8","ItemExpression",
            "JoinAlgorithm","JoinKind","JoinSide",
            "Json","Lines","List","Logical","MissingField",
            "Module","None","Null","Number","OData",
            "ODataOmitValues","Occurrence","Order","Password",
            "Percentage","PercentileMode","Precision","Progress",
            "QuoteStyle","RankKind","Record","RelativePosition",
            "Replacer","Resource","RoundingMode","RowExpression",
            "Single","Splitter","Table","Tables","Text",
            "TextEncoding","Time","TimeSeriesInsights",
            "TimeZone","TraceLevel","Type","Uri","Value",
            "Variable","Web","WebAction","WebMethod","Xml"
            },

    Source = 
        Table.AddIndexColumn(
            Table.SelectRows(
                Table.Sort(
                    Table.FromList(Record.FieldNames(#shared), Splitter.SplitByNothing(), null, null, ExtraValues.Error),
                    {{"Column1", Order.Ascending}}
                ), 
                each if SelectionListType = "Allow" then List.AnyTrue(List.Transform(SelectionList, (prefix) => Text.StartsWith([Column1], prefix))) else  not List.AnyTrue(List.Transform(SelectionList, (prefix) => Text.StartsWith([Column1], prefix)))
            ), 
            "Index", 0, 1, Int64.Type
        ) 
in
    Table.AddColumn(Source, "Functions", each if [Index] = List.Max(Source[Index]) then [Column1] & "=" & [Column1] else [Column1] & "=" & [Column1] & ",")[Functions]
in 
    Value.ReplaceType(fn, Value.ReplaceMetadata(Value.Type(fn), doc))
